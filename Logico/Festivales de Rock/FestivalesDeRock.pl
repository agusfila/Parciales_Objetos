anioActual(2015).


%festival(nombre, lugar, bandas, precioBase).

%lugar(nombre, capacidad).
festival(lulapaluza, lugar(hipodromo,40000), [miranda, paulMasCarne, muse], 500).
festival(mostrosDelRock, lugar(granRex, 10000), [kiss, judasPriest, blackSabbath], 1000).
festival(personalFest, lugar(geba, 5000), [tanBionica, miranda, muse, pharrellWilliams], 300).
festival(cosquinRock, lugar(aerodromo, 2500), [erucaSativa, laRenga], 100).


%banda(nombre, año, nacionalidad, popularidad).
banda(paulMasCarne,1960, uk, 70).
banda(muse,1994, uk, 45).
banda(kiss,1973, us, 63).
banda(erucaSativa,2007, ar, 60).
banda(judasPriest,1969, uk, 91).
banda(tanBionica,2012, ar, 71).
banda(miranda,2001, ar, 38).
banda(laRenga,1988, ar, 70).
banda(blackSabbath,1968, uk, 96).
banda(pharrellWilliams,2014, us, 85).

%entradasVendidas(nombreDelFestival, tipoDeEntrada, cantidadVendida).
% tipos de entrada: campo, plateaNumerada(numero de fila), plateaGeneral(zona).
entradasVendidas(lulapaluza,campo, 600).
entradasVendidas(lulapaluza,plateaGeneral(zona1), 200).
entradasVendidas(lulapaluza,plateaGeneral(zona2), 300).
entradasVendidas(mostrosDelRock,campo,20000).
entradasVendidas(mostrosDelRock,plateaNumerada(1),40).
entradasVendidas(mostrosDelRock,plateaNumerada(2),0).
entradasVendidas(cosquinRock,campo, 600).
entradasVendidas(mostrosDelRock,plateaNumerada(10),25).
entradasVendidas(mostrosDelRock,plateaGeneral(zona1),300).
entradasVendidas(mostrosDelRock,plateaGeneral(zona2),500).

plusZona(hipodromo, zona1, 55).
plusZona(hipodromo, zona2, 20).
plusZona(granRex, zona1, 45).
plusZona(granRex, zona2, 30).
plusZona(aerodromo, zona1, 25).

estaDeModa(Banda) :-
	bandaReciente(Banda),
	popularidadMayorA(70,Banda).

bandaReciente(Banda) :-
	anioActual(AnioActual),
	banda(Banda,AnioCreacion,_,_),
	AniosDeVida is AnioActual - AnioCreacion, 
	AniosDeVida =< 5.

popularidadMayorA(PopularidadMinima,Banda) :-
	banda(Banda,_,_,Popularidad),
	Popularidad > PopularidadMinima.

esCareta(Festival) :-
	festival(Festival,_,Bandas,_),
	member(Banda,Bandas),
	member(OtraBanda,Bandas),
	Banda \= OtraBanda,
	estaDeModa(Banda),
	estaDeModa(OtraBanda).
esCareta(Festival) :-
	festival(Festival,_,Bandas,_),
	member(miranda,Bandas).
esCareta(Festival) :-
	festival(Festival,_,_,_),
	forall(entradasVendidas(Festival,Entrada,_),not(entradaRazonable(Festival,Entrada))).

entradaRazonable(Festival,Entrada) :-
	entradasVendidas(Festival,Entrada,_),
	esRazonable(Festival,Entrada).

esRazonable(Festival,plateaGeneral(Zona)) :-
	precio(Festival,plateaGeneral(Zona),PrecioTotal),
	festival(Festival,lugar(Lugar,_),_,_),
	plusZona(Lugar,Zona,PlusZona),
	DiezPorCiento is PrecioTotal * 0.1,
	PlusZona < DiezPorCiento.
esRazonable(Festival,campo) :-
	precio(Festival,campo,PrecioTotal),
	popularidadTotal(Festival,Popularidad),
	PrecioTotal < Popularidad.
esRazonable(Festival,plateaNumerada(Fila)) :-
	precio(Festival,plateaNumerada(Fila),PrecioTotal),
	festival(Festival,_,Bandas,_),
	forall(member(Banda,Bandas),not(estaDeModa(Banda))),
	PrecioTotal =< 750.
esRazonable(Festival,plateaNumerada(Fila)) :-
	precio(Festival,plateaNumerada(Fila),PrecioTotal),
	festival(Festival,lugar(_,Capacidad),Bandas,_),
	popularidadTotal(Festival,Popularidad),
	member(Banda,Bandas),
	estaDeModa(Banda),
	PrecioTotal =< 750,
	PrecioMaximo is Capacidad // Popularidad,
	PrecioTotal < PrecioMaximo.

precio(Festival,campo,Precio) :-
	festival(Festival,_,_,Precio).
precio(Festival,plateaGeneral(Zona),Precio) :-
	festival(Festival,lugar(Lugar,_),_,PrecioBase),
	plusZona(Lugar,Zona,PlusZona),
	Precio is PrecioBase + PlusZona.
precio(Festival,plateaNumerada(Fila),Precio) :-
	entradasVendidas(Festival,plateaNumerada(Fila),_),
	festival(Festival,_,_,PrecioBase),
	Precio is PrecioBase + (200 // Fila).

popularidadTotal(Festival,Popularidad) :-
	festival(Festival,_,Bandas,_),
	findall(Popularidad,(member(Banda,Bandas),banda(Banda,_,_,Popularidad)),Popularidades),
	sumlist(Popularidades,Popularidad).
	
nacanpop(Festival) :-
	entradasVendidas(Festival,Entrada,_),
	entradaRazonable(Festival,Entrada),
	festival(Festival,_,Bandas,_),
	forall(member(Banda,Bandas),banda(Banda,_, ar,_)).

recaudacion(Festival,RecaudacionTotal) :-
	festival(Festival,_,_,_),
	findall(Recaudacion,(entradasVendidas(Festival,Entrada,Cantidad),calcularRecaudacion(Festival,Entrada,Cantidad,Recaudacion)),Recaudaciones),
	sumlist(Recaudaciones,RecaudacionTotal).

calcularRecaudacion(Festival,Entrada,Cantidad,Recaudacion) :-
	precio(Festival,Entrada,Precio),
	Recaudacion is Precio * Cantidad.

estaBienPlaneado/1. Se cumple si las bandas que participan van creciendo en popularidad,
y la banda que cierra el festival (es decir, la última) es legendaria.
Una banda es legendaria cuando surgió antes de 1980, es internacional y tiene una popularidad mayor a la de todas las bandas de moda.

estaBienPlaneado(Festival) :-
	festival(Festival,_,Bandas,_),
	crecenEnPopularidad(0,Bandas),
	ultimaBanda(Festival,Banda),
	legendaria(Banda).

crecenEnPopularidad(PopularidadAnterior,[Banda | Bandas]) :-
	banda(Banda,_,_,Popularidad),
	Popularidad > PopularidadAnterior,
	crecenEnPopularidad(Popularidad,Bandas).
crecenEnPopularidad(_,[]).

ultimaBanda(Festival,Banda) :-
	festival(Festival,_,Bandas,_),
	length(Bandas,Cant),
	nth1(Cant,Bandas,Banda).

legendaria(Banda) :-
	banda(Banda,Anio,Nacionalidad,Popularidad),
	Nacionalidad \= ar,
	Anio < 1980,
	forall((estaDeModa(OtraBanda),banda(OtraBanda,_,_,OtraPopularidad)),OtraPopularidad =< Popularidad).